// apply - set android plugin
apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"
    defaultConfig {
        // define application pakage name - id in google store
        applicationId "org.osori.androidstudy"
        // support min sdk
        minSdkVersion 19
        // up up
        targetSdkVersion 26
        // app upgrade criteria
        versionCode 1
        // A string representing the version of the application distributed to the user.
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {

        }
        release {

        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true

            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'mips', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }

        density {
            enable true
            reset()
        }


    }

    lintOptions {
        abortOnError false
    }
    realm {
        syncEnabled = true;
    }

    productFlavors {
        arrogant {
            applicationId 'org.osori.androidstudy.arrogant'
        }
        friendly {
            applicationId 'org.osori.androidstudy.friendly'
        }
        obsequious {
            applicationId 'org.osori.androidstudy.obsequious'
        }
    }
}

ext {
    println 'app ext is called'
}

// add external library
dependencies {
    def SupportLibVer = '26.0.0'


    // define fileTree dependency and reference libs all jar files
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // if you want to import lib not in maven but local, use files or filetree grammer
    // compile files('libs/a.jar', 'libs/b.jar')
    // compile fileTree(dir: 'libs', include: '*.jar')

    // compile 'org.codehaus.groovy:groovy-all:2.4.4@jar'
    // is same as
    // compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4', ext: 'jar'

    // if you want to remove transitive dependency
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.2.0', transitive: false

    // exclude specific library in transitive dependency
    compile ('com.github.JunsuLime:Android-SectionAdapter:v0.2.0') {
        exclude group: 'junit'
        exclude group: 'com.android.support'
        transitive = false
    }

    // compile ('group:name:version:ext')
    compile ('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = false
    }

    // download junit and reference it in testCompile process
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$SupportLibVer"
    compile "com.android.support:recyclerview-v7:$SupportLibVer"

    // week2
    compile "com.android.support:design:$SupportLibVer"

    // how to release my library
//    compile 'com.github.JunsuLime:TestLibrary:v1.0.0'

    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'

    // view binding library butter knife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Image loading library glide
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'

    //////////////////////////// test libraries

    // Material dialog
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'

    // 트립 선택 dialog 에 사용됨
    compile 'com.github.traex.rippleeffect:library:1.3'

    // 트립 생성시 달력에 사용됨
    compile 'com.wdullaer:materialdatetimepicker:2.5.0'

    // ObservableRecyclerView
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'

    // TripContent header decoration
    compile 'ca.barrenechea.header-decor:header-decor:0.2.8'

    compile 'com.airbnb:deeplinkdispatch:3.0.0'

    // Rounded ImageView
    compile 'com.makeramen:roundedimageview:2.3.0'

    compile 'com.facebook.android:facebook-android-sdk:4.+'

    // photo image view
    compile 'com.github.chrisbanes:PhotoView:2.0.0'

    compile 'com.google.android.gms:play-services-auth:10.0.1'
    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile 'com.google.android.gms:play-services-drive:10.0.1'

    compile 'com.google.firebase:firebase-appindexing:10.0.0'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'

    // conn with server ...
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    // for retrofit debugging
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'

    // 글꼴 적용
    // compile 'com.tsengvn:typekit:1.0.1'

    // MainActivity bottom tab library
    compile 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.1-rc1'

    // core library of PinOnMap
    compile 'com.mapzen.tangram:tangram:0.9.0'

    // TimelineFragment viewpager 의 indicator
    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1.1@aar'

    // MyMapFragment post button
    compile 'com.getbase:floatingactionbutton:1.10.1'
}

task printProperties() {
    println "build printProperties is called"
    doLast {
        println "printProperties.doLast is called"
        println "$login"
        println "$password"
    }
}

